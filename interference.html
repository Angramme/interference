<!DOCTYPE html>
<html>
    <head>
        <title>Interferences!</title>
        
        <!--Libraries-->
        <script src="three.min.js"></script>
        <script src="dat.gui.js"></script>
        <script src="pass_scene.js"></script>


        <style>
            body { margin: 0; overflow: hidden; }
			canvas { width: 100%; height: 100%; position: fixed; left: 0; top: 0; }
            #signature { position: fixed; bottom: 0; left: 0; font-size: 1.5vh; }
        </style>
    </head>
    <body>
        <canvas id="CAN"></canvas>
        <canvas id="CAN2"></canvas>

        <div id="signature">Kacper Ozieblowski 2019-2020</div>

        <!--shaders-->
        <script id="vertexShader" type="x-shader/x-vertex">
			precision mediump float;
            precision mediump int;
            
			uniform mat4 modelViewMatrix;
            uniform mat4 projectionMatrix;
            
			attribute vec3 position;
            attribute vec4 color;
            
            varying vec3 vPosition;
            
			void main()	{
				vPosition = position;
				gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
			}
		</script>

		<script id="fragmentShader" type="x-shader/x-fragment">
			precision mediump float;
            precision mediump int;
            
            uniform float propagation_speed;
            uniform float wave_frequency;
            uniform float time;
            uniform vec2 pointers[10];
            uniform int pointerCount;
            
			varying vec3 vPosition;
            
			void main()	{
                vec2 uv = vPosition.xy;

                vec3 color = vec3(0,0,0);
            
                for(int i=0; i<10; i++){
                    if(i>=pointerCount)break;

                    vec2 p = pointers[i];
                    float d = distance(uv, p);
                    color += sin(d*wave_frequency - time*propagation_speed) /(d*5.+1.);
                }

                color = color*.25+.5;

				gl_FragColor = vec4(color, 1);
			}
        </script>

        <script id="waterVS" type="x-shader/x-vertex">
			precision mediump float;
            precision mediump int;
            
			uniform mat4 modelViewMatrix;
            uniform mat4 projectionMatrix;
            
			attribute vec3 position;
            attribute vec4 color;
            
            varying vec3 vPosition;
            
			void main()	{
				vPosition = position;
				gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
			}
		</script>

		<script id="waterFS" type="x-shader/x-fragment">
			precision mediump float;
            precision mediump int;
            
            uniform float time;
            uniform sampler2D tPrevious;
            uniform vec2 tRes;
            
            varying vec3 vPosition;
            
            vec3 sample(vec2 uv){
                return texture2D(tPrevious, uv).rgb;
            }

            vec3 normal(vec2 uv){
                vec3 eps = vec3(1./tRes, 0.);
                vec2 un = (vec2(
                    sample(uv+eps.xz).x,
                    sample(uv+eps.zy).x
                )-sample(uv).x);

                return normalize(vec3(un, .4));
            }

            vec3 sky(vec3 d){
                float v = d.z;
                return vec3(mix(1., .72, v), mix(1., .8, v), 1);
            }
            
			void main()	{
                vec2 uv = vPosition.xy*.5 + .5;
                uv.y = 1.-uv.y;

                vec3 p = vec3(
                    uv.x,
                    uv.y*tRes.y/tRes.x,
                    sample(uv).x*.4
                );

                vec3 color = vec3(0,0,0);
            
                vec3 n = normal(uv);
                vec3 L = normalize(vec3(.75, .25, .7) - p);

    
                //material color
                const vec3 mc = vec3(.9, .9, 1.);
                //light color
                const vec3 lc = vec3(1, .9, .9);
                
                //diffuse
                color += max(dot(L, n), 0.) * mc * lc * .3;

                //specular
                color += pow(max(dot(L, reflect(vec3(0,0,-1), n)), 0.), 2056.) * mc * lc * 2.2;

                //sky reflection
                vec3 rd = reflect(vec3(0,0,-1), n);
                color += sky(rd) * mc * .35;
                
                //ground refraction
                const float ground_dist = .5;
                vec3 refracted = refract(vec3(0,0,-1), n, 1.3325);
                vec3 rp = p + refracted * (ground_dist/refracted.z);
                color += ( mod(dot(floor(rp.xy*30.), vec2(1)), 2.) == 0. ? vec3(.8) : vec3(.5) ) * mc * .3;

                //color *= 1.5;
                color = pow(color, vec3(2.2));

				gl_FragColor = vec4(color, 1);
			}
        </script>

        <script id="nowaterFS" type="x-shader/x-fragment">
			precision mediump float;
            precision mediump int;
            
            uniform float time;
            uniform sampler2D tPrevious;
            uniform vec2 tRes;
            
            varying vec3 vPosition;
            
            vec3 sample(vec2 uv){
                return texture2D(tPrevious, uv).rgb;
            }
            
			void main()	{
                vec2 uv = vPosition.xy*.5 + .5;
                uv.y = 1.-uv.y;
                gl_FragColor = vec4(sample(uv)*2.0-1.0, 1);
			}
        </script>
        

        <!--actual code-->  
        <script src="main.js"></script>

    </body>
</html>